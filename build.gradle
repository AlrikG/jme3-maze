apply plugin: 'java'

// select one source-code option
//sourceCompatibility = '1.7'
sourceCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        //debug true
        enableAssertions true
        jvmArgs '-XX:+UseConcMarkSweepGC'
        //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

// select one version of the Engine
//ext.jmonkeyengineVersion = '3.2.1-stable' // from jcenter
ext.jmonkeyengineVersion = '3.2.2-beta1' // from jcenter
//ext.jmonkeyengineVersion = '3.3.0-SNAPSHOT' // from mavenLocal

if (!hasProperty('mainClass')) {
    ext.mainClass = 'jme3maze.MazeGame'
}
jar.manifest.attributes(
    'Main-Class': ext.mainClass
)

repositories {
    //mavenLocal()
    maven { url 'https://dl.bintray.com/stephengold/jme3utilities' }
    maven { url 'https://dl.bintray.com/stephengold/org.jmonkeyengine' }
    maven { url 'https://dl.bintray.com/stephengold/tonegod' }
    jcenter()
    //mavenCentral()
}

dependencies {
    // from stephengold's bintray repositories:
    compile 'tonegod:tonegodgui:0.1.1'
    compile 'jme3utilities:jme3-utilities-debug:0.9.8'
    compile 'jme3utilities:jme3-utilities-heart:2.17.0'
    compile 'jme3utilities:jme3-utilities-ui:0.6.7'
    compile 'jme3utilities:jme3-utilities-x:0.2.7'
    runtime 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    // from jcenter (or mavenLocal) repositories:
    compile "org.jmonkeyengine:jme3-blender:$jmonkeyengineVersion"
    runtime "org.jmonkeyengine:jme3-lwjgl3:$jmonkeyengineVersion"
    runtime "org.jmonkeyengine:jme3-plugins:$jmonkeyengineVersion"
}

// cleanup tasks
clean { dependsOn 'cleanDLLs', 'cleanSOs', 'cleanWrittenAssets' }
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}

task runAssetProcessor(type: JavaExec) {
    main 'jme3maze.AssetProcessor'
}