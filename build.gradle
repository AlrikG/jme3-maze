apply plugin: 'java'

// select one source-code (JDK) option
//sourceCompatibility = '1.7'
sourceCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // runtime options:
        classpath sourceSets.main.runtimeClasspath
        //debug true
        //doFirst { println commandLine }
        enableAssertions true
        jvmArgs '-XX:+UseConcMarkSweepGC'
        //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

ext.jmonkeyengineVersion = '3.2.2-stable' // from jcenter

if (!hasProperty('mainClass')) {
    ext.mainClass = 'jme3maze.MazeGame'
}

repositories {
    //mavenLocal()
    maven { url 'https://dl.bintray.com/stephengold/jme3utilities' }
    maven { url 'https://dl.bintray.com/stephengold/org.jmonkeyengine' }
    maven { url 'https://dl.bintray.com/stephengold/tonegod' }
    jcenter()
}

dependencies {
    // from stephengold's bintray repositories:
    compile 'jme3utilities:jme3-utilities-heart:2.22.0'
    compile 'jme3utilities:jme3-utilities-ui:0.7.1'
    compile 'jme3utilities:jme3-utilities-x:0.2.9'
    runtime 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'
    compile 'tonegod:tonegodgui:0.1.2'

    // from jcenter repositories:
    runtime "org.jmonkeyengine:jme3-blender:$jmonkeyengineVersion"
    runtime "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengineVersion"
    runtime "org.jmonkeyengine:jme3-plugins:$jmonkeyengineVersion"
}

task runAssetProcessor(type: JavaExec) {
    main 'jme3maze.AssetProcessor'
}

// cleanup tasks
clean { dependsOn 'cleanDLLs', 'cleanSOs', 'cleanWrittenAssets' }
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}
